" -----------------------------
"   callmekohei's vimrc
" -----------------------------

" prepare {{{

if &compatible
  set nocompatible
endif

" encoding
set encoding=utf-8
set fileencoding=utf-8
set fileencodings=ucs-boms,utf-8,euc-jp,cp932
set fileformats=unix,dos,mac
if !has('gui_running') && &encoding != 'utf-8'
  set termencoding=utf-8
endif

" vimrc's scriptencoding
scriptencoding utf-8

" create autocmd group
augroup callmekohei-vimrc
  autocmd!
augroup END

" Use English interface.
language message C

" }}}
" prepare for gvim {{{

" Disable menu.vim
if has('gui_running')
  set guioptions=Mc
endif

" }}}
" plugins {{{

autocmd callmekohei-vimrc CursorHold *.toml syntax sync minlines=300

let s:dein_dir = expand('~/.cache/dein')
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'

if &runtimepath !~# '/dein.vim'
  if !isdirectory(s:dein_repo_dir)
    execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
  endif
  execute 'set runtimepath^=' . fnamemodify(s:dein_repo_dir, ':p')
endif

if dein#load_state(s:dein_dir)
  call dein#begin(s:dein_dir)
  let s:toml_dir = expand('~/.config/nvim')
  call dein#load_toml(s:toml_dir . '/dein.toml'    , {'lazy': 0})
  call dein#load_toml(s:toml_dir . '/deinlazy.toml', {'lazy': 1})
  if !has('nvim')
    call dein#add('roxma/nvim-yarp')
    call dein#add('roxma/vim-hug-neovim-rpc')
    call dein#add('callmekohei/tigaDebugger')
  endif
  call dein#end()
  call dein#save_state()
endif

if dein#check_install()
  call dein#install()
endif

" turn plugins on
syntax on
filetype plugin indent on


" }}}
" optins {{{

"---------------------------------------------------------------------------
" Folding:
"
set foldmethod=marker
set foldcolumn=3

"---------------------------------------------------------------------------
" Search:
"
set ignorecase
set smartcase
set incsearch
set hlsearch
set wrapscan

"---------------------------------------------------------------------------
" Edit:
"
set tabstop=4 softtabstop=4 shiftwidth=4
set expandtab smarttab
set autoindent "smartindent
set backspace=indent,eol,start
set noshowmatch
set wildmenu
" テキスト挿入中の自動折り返しを日本語に対応させる
set formatoptions+=mM

"---------------------------------------------------------------------------
" View:
"
" 行番号を非表示 (number:表示)
set number
" ルーラーを表示 (noruler:非表示)
set noruler
" タブや改行を非表示 (list:表示)
set list
" どの文字でタブや改行を表示するかを設定
set listchars=tab:▸\ ,trail:-,extends:»,precedes:«,nbsp:%
" 長い行を折り返して表示 (nowrap:折り返さない)
set nowrap
" 常にステータス行を表示 (詳細は:he laststatus)
set laststatus=2
" コマンドラインの高さ (Windows用gvim使用時はgvimrcを編集すること)
set cmdheight=2
" コマンドをステータス行に表示（noshowcmd；非表示）
set noshowcmd
" タイトルを表示
set title
" Color scheme（MacVim使用時はgvimrcを編集すること）
set background=dark
colorscheme Apprentice

"---------------------------------------------------------------------------
" ファイル操作に関する設定:
"
" Don't create backup.
set nowritebackup
set nobackup
set noswapfile
set backupdir-=.


"---------------------------------------------------------------------------
" マウスに関する設定:
"
" どのモードでもマウスを使えるようにする
set mouse=a
" マウスの移動でフォーカスを自動的に切替えない (mousefocus:切替る)
set nomousefocus
" 入力時にマウスポインタを隠す (nomousehide:隠さない)
set nomousehide
" ビジュアル選択(D&D他)を自動的にクリップボードへ (:help guioptions_a)
"set guioptions+=a

"---------------------------------------------------------------------------
" Clipboard:
"
" Use clipboard register.
if (!has('nvim') || $DISPLAY != '') && has('clipboard')
  if has('unnamedplus')
    set clipboard& clipboard+=unnamedplus
  else
    set clipboard& clipboard+=unnamed
  endif
endif


"---------------------------------------------------------------------------
" Others:
"

" stop rendering duraing command executing
" set lazyredraw

" Display another buffer when current buffer isn't saved.
set hidden

" Disable tabline.
" set showtabline=0

" Turn down a long line appointed in 'breakat'
" set linebreak
" set showbreak=\
" set breakat=\ \ ;:,!?

" Disable bell.
" set t_vb=
" set novisualbell
" set belloff=all

" Maintain a current line at the time of movement as much as possible.
" set nostartofline

" Splitting a window will put the new window below the current one.
set splitbelow

" Splitting a window will put the new window right the current one.
set splitright

" Adjust window size of preview and help.
" set previewheight=8
" set helpheight=12

" fast mouse scroll
set ttyfast

" When a line is long, do not omit it in @.
" set display=lastline

" Enable virtualedit in visual block mode.
" set virtualedit=block

" Disable modeline.
" set modelines=0
" set nomodeline


" }}}
" key mappings {{{

"----------------------------------------------------------
" Searching
"

" Cancel hilight search
nnoremap <silent><Esc><Esc> :<C-u>nohlsearch<CR><C-l>

" Search by selected word/sentence
" Practical Vim (ja) : https://pragprog.com/titles/dnvim/source_code
xnoremap * :<C-u>call <SID>VSetSearch()<CR>/<C-R>=@/<CR><CR>
xnoremap # :<C-u>call <SID>VSetSearch()<CR>?<C-R>=@/<CR><CR>
function! s:VSetSearch()
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g')
  let @s = temp
endfunction


"----------------------------------------------------------
" Moving
"

" Editor lines
nnoremap j gj
nnoremap k gk

" buffers
nnoremap <silent> <C-j> :bprevious<CR>
nnoremap <silent> <C-k> :bnext<CR>

" Switch CWD to the directory of the open buffer
nnoremap <leader>cd :cd %:p:h<cr>:pwd<cr>


"----------------------------------------------------------
" Editing
"
nnoremap Y y$


"----------------------------------------------------------
" Folding
"

noremap [space] <nop>
nmap <Space> [space]

noremap [space]j zj
noremap [space]k zk
noremap [space]n ]z
noremap [space]p [z
noremap [space]h zc
noremap [space]l zo
noremap [space]a za
noremap [space]m zM
noremap [space]i zMzv
noremap [space]r zR
noremap [space]f zf

" max window size
" https://qiita.com/grohiro/items/e3dbcc93510bc8c4c812
let g:toggle_window_size = 0
function! ToggleWindowSize()
  if g:toggle_window_size == 1
    exec "normal \<C-w>="
    let g:toggle_window_size = 0
  else
    :resize
    :vertical resize
    let g:toggle_window_size = 1
  endif
endfunction
nnoremap M :call ToggleWindowSize()<CR>


" temporary commands
nnoremap <Space>. :edit $MYVIMRC<CR>
command! Memo     : edit $HOME/tmp/memo_folder/memo.txt | : silent cd %:p:h | :silent pwd


" }}}
" filetypes {{{

" General, Text, Markdown {{{

  autocmd callmekohei-vimrc BufNewFile,BufRead *.{md,mdwn,mkd,mkdn,mark*} set filetype=markdown
  autocmd callmekohei-vimrc FileType text,markdown setlocal ambiwidth=double wrap display=lastline

  " Remove space at end of line ( exception: markdown, text )
  autocmd callmekohei-vimrc BufWritePre * if index(['markdown','text'], &ft)==-1 | :%s/\s\+$//e | endif

" }}}


" Toml {{{

  autocmd callmekohei-vimrc FileType toml setlocal tabstop=2 softtabstop=2 shiftwidth=2

" }}}


" Vim script {{{

  autocmd callmekohei-vimrc BufNewFile,BufRead vimrc,gvimrc setfiletype vim
  autocmd callmekohei-vimrc FileType vim setlocal tabstop=2 softtabstop=2 shiftwidth=2

" }}}


" QuickFix {{{

  " code from : https://gist.github.com/juanpabloaj/5845848#file-adjustwindowheight-vim
  autocmd callmekohei-vimrc FileType qf call AdjustWindowHeight(3, 10)
  function! AdjustWindowHeight(minheight, maxheight)
    let l = 1
    let n_lines = 0
    let w_width = winwidth(0)
    while l <= line('$')
    " number to float for division
    let l_len = strlen(getline(l)) + 0.0
    let line_width = l_len/w_width
    let n_lines += float2nr(ceil(line_width))
    let l += 1
    endw
    exe max([min([n_lines, a:maxheight]), a:minheight]) . "wincmd _"
  endfunction

" }}}


" Bash script {{{

  autocmd callmekohei-vimrc FileType sh call s:vimrc_bash()
  function! s:vimrc_bash() abort
    let g:is_bash = 1
    let g:sh_no_error = 1
    let &errorformat = '%f:\ line\ %l:\ %m'
  endfunction

  " quickrun settings
  let g:quickrun_config.sh = {}
  let g:quickrun_config.sh = { 'command': 'bash' }
  let g:quickrun_config.bashCheck = {}
  let g:quickrun_config.bashCheck = {
    \  'exec'                            : [ '%c -n %s:p:r.bash' ]
    \ ,'command'                         : 'bash'
    \ ,'hook/time/enable'                : 0
    \ ,"outputter/buffer/close_on_empty" : 1
    \ ,"outputter"                       : 'quickfix'
    \ ,'outputter/buffer/split'          : ':set splitblow',
  \}
  autocmd callmekohei-vimrc BufWinEnter,BufWritePost *.bash call quickrun#run( g:quickrun_config.bashCheck )

" }}}


" dependencies {{{

  autocmd callmekohei-vimrc BufNewFile,BufRead *.dependencies call s:vimrc_dependencies()
  function! s:vimrc_dependencies() abort
    setlocal filetype=dependencies
    setlocal dictionary+=~/tmp/mydictionary/foo_dictionary
  endfunction

" }}}


" Python {{{

  let g:quickrun_config.python = {}
  let g:quickrun_config.python = { 'command': 'python3' }
  autocmd callmekohei-vimrc BufRead,BufNewFile *.pdbrc setfiletype python

" }}}


" }}}
" nvim {{{

let g:python_host_prog  = '/usr/local/bin/python2'
let g:python3_host_prog = '/usr/local/bin/python3'

if exists('&inccommand')
  set inccommand=nosplit
endif

" }}}
" mac {{{

" Macではデフォルトの'iskeyword'がcp932に対応しきれていないので修正
set iskeyword=@,48-57,_,128-167,224-235

" }}}
" local {{{

" plugin setting
call textobj#user#plugin('yyy', {} )
call textobj#user#plugin('yyy', {
\ 'bbb': {
\ 'pattern': ["^hook.* = '''","^'''"],
\ 'select-a': 'aP',
\ 'select-i': 'iP',
\ },
\ })


command! Space2from4 call s:space4_to_space2()

function! s:space4_to_space2() abort
  set ts=4 sts=4 noet
  retab!
  set ts=2 sts=2 et
  retab
endfunction


command! Space4from2 call s:space2_to_space4()

function! s:space2_to_space4() abort
  set ts=2 sts=2 noet
  retab!
  set ts=4 sts=4 et
  retab
endfunction




" }}}
