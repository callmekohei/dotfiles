" callmekohei's vimrc

" option flag
let s:ON  = 1
let s:OFF = 0

" prepare {{{1

  if &compatible
    let &compatible = s:OFF
  endif

  " Make the vim option the same as the neovim option {{{2
  if !has('nvim')

    " :h nvim-defaults

    let &autoindent     = s:ON
    let &autoread       = s:ON
    let &backspace      = 'indent,eol,start'
    let &backupdir      = '.' . ',' . expand('~/.local/share/nvim/backup',1)
    let &belloff        = 'all'
    let &complete       = '.,w,b,u,t'
    let &cscopeverbose  = s:ON
    let &directory      = expand('~/.local/share/nvim/swap//',1)
    let &display        = 'lastline,msgsep'
    let &encoding       = 'utf-8'
    let &fillchars      = "vert:│,fold:·"
    let &fsync          = s:OFF
    let &formatoptions  = 'jcroql'
    let &history        = 10000
    let &hlsearch       = s:ON
    let &incsearch      = s:ON
    let &langnoremap    = s:ON
    let &langremap      = s:OFF
    let &laststatus     = 2
    let &listchars      = 'tab:> ,trail:-,nbsp:+'
    let &nrformats      = 'bin,hex'
    let &ruler          = s:ON
    let &sessionoptions = 'blank,buffers,curdir,folds,help,tabpages,winsize'
    let &showcmd        = s:ON
    let &sidescroll     = s:ON
    let &smarttab       = s:ON
    let &tabpagemax     = 50
    let &tags           = './tags,tags'
    let &ttyfast        = s:ON
    let &undodir        = expand('~/.local/share/nvim/undo',1)
    let &viminfo        = "!,'100,<50,s10,h"
    let &wildmenu       = s:ON

  endif
  " }}}2

  " encoding
  let &encoding      = 'utf-8'
  let &fileencoding  = 'utf-8'
  let &fileencodings = 'ucs-boms,utf-8,euc-jp,cp932'
  let &fileformats   = 'unix,dos,mac'
  if !has('gui_running') && &encoding !=# 'utf-8'
    let &termencoding = 'utf-8'
  endif

  " vimrc's scriptencoding
  scriptencoding utf-8

  " create autocmd group
  augroup callmekohei-vimrc
    autocmd!
  augroup END

  " Use English interface.
  language message C

" }}}1 -------------------------------------------------
" prepare for gvim {{{1

  " Disable menu.vim
  if has('gui_running')
    let &guioptions = 'Mc'
  endif

" }}}1 -------------------------------------------------
" plugins {{{1

  autocmd callmekohei-vimrc CursorHold *.toml syntax sync minlines=300

  let s:dein_dir = expand('~/.cache/dein',1)
  let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'

  if &runtimepath !~# '/dein.vim'
    if !isdirectory(s:dein_repo_dir)
      execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
    endif
    execute 'set runtimepath^=' . fnamemodify(s:dein_repo_dir, ':p')
  endif

  if dein#load_state(s:dein_dir)
    call dein#begin(s:dein_dir)
    let s:toml_dir = expand('~/.config/nvim',1)
    call dein#load_toml(s:toml_dir . '/dein.toml'    , {'lazy': 0})
    call dein#load_toml(s:toml_dir . '/deinlazy.toml', {'lazy': 1})
    if !has('nvim')
      call dein#add('roxma/nvim-yarp')
      call dein#add('roxma/vim-hug-neovim-rpc')
      call dein#add('callmekohei/tigaDebugger')
    endif
    call dein#end()
    call dein#save_state()
  endif

  if dein#check_install()
    call dein#install()
  endif

  " turn plugins on
  syntax on
  filetype plugin indent on

" }}}1 -------------------------------------------------
" colorscheme {{{1

  function! s:myHighlights() abort
    highlight Comment       ctermfg=245   guifg=#8a8a8a
    highlight FoldColumn    ctermfg=245   guifg=#8a8a8a
    highlight Folded        ctermfg=245   guifg=#8a8a8a
    highlight OptionKeyword ctermfg=103   guifg=#8787af
    highlight UnicodeSpaces ctermbg=131   guibg=#af5f5f
  endfunction
  autocmd callmekohei-vimrc ColorScheme * call s:myHighlights()

  let &background = 'dark'
  colorscheme Apprentice

" }}}1 -------------------------------------------------
" options {{{1

  " Search
  let &ignorecase = s:ON
  let &smartcase  = s:ON
  let &incsearch  = s:ON
  let &hlsearch   = s:ON
  let &wrapscan   = s:ON

  " Edit
  let &tabstop         = 4
  let &softtabstop     = 4
  let &shiftwidth      = 4
  let &expandtab       = s:ON
  let &smarttab        = s:ON
  let &autoindent      = s:ON
  let &smartindent     = s:OFF
  let &backspace       = 'indent,eol,start'
  let &showmatch       = s:OFF
  let &wildmenu        = s:ON
  let &formatoptions   = join( [ &formatoptions , 'mM'] , '' ) " テキスト挿入中の自動折り返しを日本語に対応させる
  let &virtualedit     = 'block'

  " View
  let &number     = s:ON
  let &ruler      = s:OFF
  let &list       = s:ON
  let &listchars  = 'tab:> ,trail:-,nbsp:+,extends:»,precedes:«,'
  let &wrap       = s:OFF
  let &laststatus = 2
  let &cmdheight  = 2
  let &showcmd    = s:OFF
  let &title      = s:ON

  " mouse
  let &mouse      = 'a'
  let &mousefocus = s:OFF
  let &mousehide  = s:OFF

  " clipboard
  if (!has('nvim') || $DISPLAY !=# '') && has('clipboard')
    if has('unnamedplus')
      let &clipboard = 'unnamedplus'
    else
      let &clipboard = 'unnamed'
    endif
  endif

  " others
  let &lazyredraw = s:ON
  let &hidden     = s:ON
  let &splitright = s:ON
  let &ttyfast    = s:ON

  " Disable bell.
  let &t_vb       = ''
  let &visualbell = s:OFF
  let &belloff    = 'all'

  " Don't create backup.
  let &writebackup = s:OFF
  let &backup      = s:OFF
  let &swapfile    = s:OFF
  set backupdir-=.

" }}}1 -------------------------------------------------
" key mappings {{{1

  let g:mapleader      = "\<Space>"
  let g:maplocalleader = "\<Space>"

  " open file wiht line number
  nnoremap gf gF
  vnoremap gf gF

  nmap <leader><leader> gx
  " URL ( 使わなかったら破棄 ）

  " Avoid key mistakes
  nnoremap qqq: <Esc>q:
  nnoremap qqq/ <Esc>q/
  nnoremap q: <Nop>
  nnoremap q/ <Nop>
  nnoremap q? <Nop>


  " Folding ( http://d.hatena.ne.jp/leafcage/20111223/1324705686 )
  noremap <leader>j zj
  noremap <leader>k zk
  noremap <leader>n ]z
  noremap <leader>p [z
  noremap <leader>h zc
  noremap <leader>l zo
  noremap <leader>a za
  noremap <leader>m zM
  noremap <leader>i zMzv
  noremap <leader>r zR
  noremap <leader>f zf

  " Cancel hilight search
  nnoremap <silent><Esc><Esc> :<C-u>nohlsearch<CR><C-l>

  " Search by selected word/sentence ( Practical Vim (ja) : https://pragprog.com/titles/dnvim/source_code )
  xnoremap * :<C-u>call <SID>vSetSearch()<CR>/<C-R>=@/<CR><CR>
  xnoremap # :<C-u>call <SID>vSetSearch()<CR>?<C-R>=@/<CR><CR>
  function! s:vSetSearch()
    let temp = @s
    normal! gv"sy
    let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g')
    let @s = temp
  endfunction

  " Editor lines
  nnoremap j gj
  nnoremap k gk

  " buffers
  nnoremap <silent> <C-j> :bprevious<CR>
  nnoremap <silent> <C-k> :bnext<CR>

  " Switch CWD to the directory of the open buffer
  nnoremap <leader>cd :cd %:p:h<cr>:pwd<cr>

  " yank half
  nnoremap Y y$

  " expand window ( https://qiita.com/grohiro/items/e3dbcc93510bc8c4c812 )
  nnoremap M :<C-u>call <SID>toggleWindowSize()<CR>
  let s:toggle_window_size = 0
  function! s:toggleWindowSize()
    if s:toggle_window_size ==# 1
      execute "normal \<C-w>="
      let s:toggle_window_size = 0
    else
      :resize
      :vertical resize
      let s:toggle_window_size = 1
    endif
  endfunction

  " vimrc
  nnoremap <leader>v :edit $MYVIMRC<CR>

  " memo
  nnoremap <leader>, :MemoNew<CR>
  nnoremap <leader>. :MemoList<CR>
  nnoremap <leader>/ :MemoGrep<CR>

" }}}1 -------------------------------------------------
" filetypes {{{1
" General {{{2

  " Add syntax of UnicodeSpaces
  autocmd callmekohei-vimrc BufNewFile,BufRead * call matchadd( 'UnicodeSpaces', '\%u180E\|\%u2000\|\%u2001\|\%u2002\|\%u2003\|\%u2004\|\%u2005\|\%u2006\|\%u2007\|\%u2008\|\%u2009\|\%u200A\|\%u2028\|\%u2029\|\%u202F\|\%u205F\|\%u3000' )

  " Trim trailing spaces
  autocmd callmekohei-vimrc BufWritePre * call s:deleteTailSpace()
  function! s:deleteTailSpace() abort

    " Replace unicode spaces to normal space
    silent! execute( ':%s/\%u00A0\|\%u180E\|\%u2000\|\%u2001\|\%u2002\|\%u2003\|\%u2004\|\%u2005\|\%u2006\|\%u2007\|\%u2008\|\%u2009\|\%u200A\|\%u2028\|\%u2029\|\%u202F\|\%u205F\|\%u3000/ /ge' )

    if index(['markdown','text'], &ft)==-1
      silent! execute( '%s/\s\+$//e' )
    endif

  endfunction

" }}}2 -------------------------------------------------
" todo {{{2

  autocmd callmekohei-vimrc FileType todo call s:vimrc_todo()
  function! s:vimrc_todo() abort
    let g:Todo_fold_char='+'
  endfunction

" }}} --------------------------------------------------
" Vim script {{{2

  autocmd callmekohei-vimrc BufNewFile,BufRead vimrc,gvimrc setfiletype vim
  autocmd callmekohei-vimrc FileType vim call s:vimrc_vim()
  function! s:vimrc_vim() abort

    let g:vim_indent_cont = 2
    let &l:tabstop        = 2
    let &l:softtabstop    = 2
    let &l:shiftwidth     = 2

    " Add syntax of let-option
    call matchadd( 'OptionKeyword', '\s&\zs\w*\ze$' )
    call matchadd( 'OptionKeyword', '\s&\zs\w*\ze\s')
    call matchadd( 'OptionKeyword', '\s&\(g:\|l:\)\zs\w*\ze\s')

  endfunction

" }}}2 -------------------------------------------------
" quickrun {{{2

  autocmd callmekohei-vimrc FileType quickrun let &l:wrap = s:ON
  autocmd FileType quickrun nnoremap <nowait><silent><buffer> q :quit<CR>

" }}}2 -------------------------------------------------
" memo {{{2

  autocmd callmekohei-vimrc BufNewFile,BufRead *.memo call s:vimrc_memo()
  function! s:vimrc_memo() abort
    let &l:filetype    = 'memo'
    let &l:foldmethod  = 'marker'
    let &l:tabstop     = 2
    let &l:softtabstop = 2
    let &l:shiftwidth  = 2
    let &l:ambiwidth   = 'double'
    let &l:wrap        = s:ON
    let &l:display     = 'lastline'
  endfunction

  " Enable syntax highlight between triple back-ticks
  let s:lst = ['fsharp','vim','sh','python']
  autocmd callmekohei-vimrc FileType memo
    \ call map( s:lst, { i,v ->
      \ call( 's:textEnableCodeSnip', [v:val,'```'.v:val,'```','SpecialComment'] )
      \ })

  " http://vim.wikia.com/wiki/Different_syntax_highlighting_within_regions_of_a_file
  function! s:textEnableCodeSnip(filetype,start,end,textSnipHl) abort "{{{3
    let ft=toupper(a:filetype)
    let group='textGroup'.ft
    if exists('b:current_syntax')
      let s:current_syntax=b:current_syntax
      " Remove current syntax definition, as some syntax files (e.g. cpp.vim)
      " do nothing if b:current_syntax is defined.
      unlet b:current_syntax
    endif
    execute 'syntax include @'.group.' syntax/'.a:filetype.'.vim'
    try
      execute 'syntax include @'.group.' after/syntax/'.a:filetype.'.vim'
    catch
    endtry
    if exists('s:current_syntax')
      let b:current_syntax=s:current_syntax
    else
      unlet b:current_syntax
    endif
    execute 'syntax region textSnip'.ft.'
    \ matchgroup='.a:textSnipHl.'
    \ start="'.a:start.'" end="'.a:end.'"
    \ contains=@'.group
  endfunction
  "}}}3

" }}}2 -------------------------------------------------
" Text {{{2

  " conflict ambiwidth=double and let &fillchars = vert:│,fold:
  " autocmd callmekohei-vimrc FileType text setlocal ambiwidth=double wrap display=lastline

  " extensionsがfooだったらfiletypeをfooにする
  autocmd callmekohei-vimrc BufNewFile,BufRead *.foo setfiletype foo
  " syntaxを追加する
  autocmd callmekohei-vimrc FileType foo call matchadd( 'Foo', 'foo' )


" }}}2 -------------------------------------------------
" Markdown {{{2

  autocmd callmekohei-vimrc BufNewFile,BufRead *.{md,mdwn,mkd,mkdn,mark*} set filetype=markdown
  autocmd callmekohei-vimrc FileType markdown setlocal ambiwidth=double wrap display=lastline

" }}}2 -------------------------------------------------
" help {{{2

  " Japanese help text
  let &helplang = 'ja,en'

  " Open Vim internal help by K command
  let &keywordprg = ':help'

  " Close by q
  autocmd FileType help nnoremap <nowait><silent><buffer>q :quit<CR>

  autocmd callmekohei-vimrc FileType help let &l:foldmethod = 'indent'

" }}}2 -------------------------------------------------
" fsharp {{{2

  " https://github.com/fsharp/vim-fsharp/pull/103
  " needs to set regexpengine before set filetype
  if !has('nvim') && !has('gui_running')
    autocmd callmekohei-vimrc BufNewFile,BufRead *.fs,*.fsi,*.fsx  let &g:regexpengine = s:ON
  endif

  autocmd callmekohei-vimrc BufNewFile,BufRead *.fs,*.fsi,*.fsx setlocal filetype=fsharp
  autocmd callmekohei-vimrc FileType fsharp call s:vimrc_fsharp()

  " TODO: needs to ajust fold settings
  function! s:vimrc_fsharp() abort
    let &l:errorformat   = '%f(%l\,%c):\ %m,%-G %.%#,%-G,%-G%[%^/]%.%#'
    let &l:previewheight = 5
    let &l:splitbelow    = s:OFF
    let &l:foldmethod    = 'indent'
    let &l:foldlevel     = 1
    let &l:foldminlines  = 3
  endfunction

  " ---------------------------------------------------------
  "                   Setting for QuickRun
  " ---------------------------------------------------------

  if has('mac')
    let s:command = 'fsharpi --readline-'
  elseif has('win32')
    let s:command = 'fsi'
  elseif has('unix')
    let s:command = 'fsharpi'
  else
    let s:command = 'fsharpi'
  endif

  let g:quickrun_config.fsharp      = {}
  let g:quickrun_config.fsharpCheck = {}
  let g:quickrun_config.fsharpi     = {}

  let g:quickrun_config.fsharp = {
    \  'command'                          : s:command
    \ ,'runner'                           : 'concurrent_process'
    \ ,'runner/concurrent_process/load'   : '#load "%S";;'
    \ ,'runner/concurrent_process/prompt' : '> '
    \ ,'hook/time/format'                 : "\n*** time : %g s ***"
    \ ,'hook/time/dest'                   : ''
    \ ,"outputter/buffer/split"           : 'vertical'
  \}

  let g:quickrun_config.fsharpCheck = {
    \  'command'                          : s:command
    \ ,'runner'                           : 'concurrent_process'
    \ ,'runner/concurrent_process/load'   : '#load "%S";;'
    \ ,'runner/concurrent_process/prompt' : '> '
    \ ,'hook/time/enable'                 : 0
    \ ,"outputter/buffer/close_on_empty"  : 1
    \ ,"outputter"                        : 'quickfix'
    \ ,'outputter/buffer/split'           : ':set splitblow',
  \}

  let g:quickrun_config.fsharpi = {
    \  'exec'    : [ '%c %s:p:r.fsx' ]
    \ ,'command' : 'fsharpi'
    \ ,'args'    : '%{input("args> ")}'
  \}

  " very unstable (^_^;;
  " https://github.com/thinca/vim-quickrun/issues/175
  if has('nvim') || has('gui_running')
    autocmd callmekohei-vimrc BufWinEnter,BufWritePost *.fsx call quickrun#run( g:quickrun_config.fsharpCheck )
  else
    autocmd callmekohei-vimrc BufWritePost *.fsx call quickrun#run( g:quickrun_config.fsharpCheck )
  endif

" }}}2 -------------------------------------------------
" dependencies {{{2

  autocmd callmekohei-vimrc BufNewFile,BufRead *.dependencies call s:vimrc_dependencies()
  function! s:vimrc_dependencies() abort
    let &l:filetype = 'dependencies'
    let &l:dictionary = join( [ &l:dictionary, expand('~/.vim/dictionary/dic_dependencies', 1) ] , ',' )
  endfunction

" }}}2 -------------------------------------------------
" Toml {{{2

  autocmd callmekohei-vimrc FileType toml setlocal tabstop=2 softtabstop=2 shiftwidth=2

" }}}2 -------------------------------------------------
" QuickFix {{{2

  " https://gist.github.com/juanpabloaj/5845848#file-adjustwindowheight-vim
  autocmd callmekohei-vimrc FileType qf call s:adjustWindowHeight(3, 10)
  function! s:adjustWindowHeight(minheight, maxheight)
    let l = 1
    let n_lines = 0
    let w_width = winwidth(0)
    while l <= line('$')
      " number to float for division
      let l_len = strlen(getline(l)) + 0.0
      let line_width = l_len/w_width
      let n_lines += float2nr(ceil(line_width))
      let l += 1
    endw
    exe max([min([n_lines, a:maxheight]), a:minheight]) . "wincmd _"
  endfunction

" }}}2 -------------------------------------------------
" Bash script {{{2

  autocmd callmekohei-vimrc FileType sh call s:vimrc_bash()
  autocmd callmekohei-vimrc BufWinEnter,BufWritePost *.bash call quickrun#run( g:quickrun_config.bashCheck )

  function! s:vimrc_bash() abort

    let g:is_bash = 1
    let g:sh_no_error = 1
    let &l:errorformat = '%f:\ line\ %l:\ %m'

    " quickrun settings
    let g:quickrun_config.sh = {}
    let g:quickrun_config.bashCheck = {}

    let g:quickrun_config.sh = { 'command': 'bash' }
    let g:quickrun_config.bashCheck = {
      \  'exec'                            : [ '%c -n %s:p:r.bash' ]
      \ ,'command'                         : 'bash'
      \ ,'hook/time/enable'                : 0
      \ ,"outputter/buffer/close_on_empty" : 1
      \ ,"outputter"                       : 'quickfix'
      \ ,'outputter/buffer/split'          : ':set splitblow',
    \}

  endfunction

" }}}2 -------------------------------------------------
" Python {{{2

  autocmd callmekohei-vimrc BufRead,BufNewFile *.pdbrc setfiletype python
  autocmd callmekohei-vimrc FileType sh call s:vimrc_python()
  function! s:vimrc_python() abort
    let g:quickrun_config.python = {}
    let g:quickrun_config.python = { 'command': 'python3' }
  endfunction
" }}}2 -------------------------------------------------
" }}}1 -------------------------------------------------
" nvim {{{1

  if has('nvim')
    let g:python_host_prog  = '/usr/local/bin/python2'
    let g:python3_host_prog = '/usr/local/bin/python3'

    if exists('&inccommand')
      let &g:inccommand = 'nosplit'
    endif

    " enable to local syntax highlight
    let &g:runtimepath = join( [ &g:runtimepath , expand(',~/.vim',1) ] , ',' )
  endif

" }}}1 -------------------------------------------------
" mac {{{1

  " Macではデフォルトの'iskeyword'がcp932に対応しきれていないので修正
  " https://github.com/koron/vim-kaoriya/blob/master/kaoriya/vim/vimrc#L221
  if has('mac')
    let &iskeyword = '@,48-57,_,128-167,224-235'
  endif

  " TODO: needs more infomations!
  " ふむ〜 日本語自動offを使うとなんとなく挙動がおかしくなる・・・
  " if has('mac')
  "   set ttimeoutlen=1
  "   let s:async = '&'
  "   let s:imeoff = 'osascript -e "tell application \"System Events\" to key code 102"' . ' ' . s:async
  "   autocmd callmekohei-vimrc InsertLeave * :call system(s:imeoff)
  "   noremap <silent> <ESC> <ESC>:call system(s:imeoff)<CR>
  " endif

" }}}1 -------------------------------------------------
" util {{{1

  " use fzf :BLines or :Lines
  " line search ( and or )
  " command! -nargs=+ AndLineSearhc call execute( '/\v'. s:lineSearchString('&',<f-args>) )
  " command! -nargs=+ OrLineSearch  call execute( '/\v'. s:lineSearchString('|',<f-args>) )
  " function! s:lineSearchString(...) abort
  "   return join(map( a:000[1:], { i,v -> '(^|.*\s)' . v . '(\s.*|$)' }) , a:000[0] )
  " endfunction

  " message window -> right buffer ( https://qiita.com/sgur/items/9e243f13caa4ff294fa8 )
  command! -nargs=+ -complete=command Capture QuickRun -type vim -src <q-args>
  command! -nargs=0 Messages2 QuickRun -type vim -src message
  command! -nargs=0 Maps2 QuickRun -type vim -src map

  " line length 60
  inoreabbrev <expr> ---- repeat('-', 60 - col('.'))

  " do center selected strings
  command! -range Center2 call s:center2(<line1>,<line2>)
  function! s:center2(fst,lst) abort
    let l:maxcol = max(map(getline(a:fst,a:lst),'strlen(v:val)'))
    execute (a:fst) "," (a:lst) "center" l:maxcol
  endfunction

  " cleare selected empty lines
  command! -range ClearEmptyLines call s:clearEmptyLines(<line1>,<line2>)
  function! s:clearEmptyLines(fst,lst) abort
    execute (a:fst) "," (a:lst) 'vglobal/\S/d'
  endfunction

  command! SelfFileDelete call delete(expand('%',1))

  command! Space2from4 call s:space2from4()
  function! s:space2from4() abort
    let &l:tabstop     = 4
    let &l:softtabstop = 4
    let &l:expandtab   = s:OFF
    retab!
    let &l:tabstop     = 2
    let &l:softtabstop = 2
    let &l:expandtab   = s:ON
    retab
  endfunction

  command! Space4from2 call s:space4from2()
  function! s:space4from2() abort
    let &l:tabstop     = 2
    let &l:softtabstop = 2
    let &l:expandtab   = s:OFF
    retab!
    let &l:tabstop     = 4
    let &l:softtabstop = 4
    let &l:expandtab   = s:ON
    retab
  endfunction

  " dein.toml に書くと tick の関係でエラーになるので vimrc に書く
  call textobj#user#plugin('yyy', {} )
  call textobj#user#plugin('yyy', {
  \ 'bbb': {
  \ 'pattern': ["^hook.* = '''","^'''"],
  \ 'select-a': 'aP',
  \ 'select-i': 'iP',
  \ },
  \ })

" }}}1 -------------------------------------------------
" __END__  "{{{1

  " must be written at the last. see :help 'secure'
  let &g:secure = s:ON

  " vim: expandtab softtabstop=2 shiftwidth=2
  " vim: foldmethod=marker
  " vim: foldcolumn=5
